import model.estadios.*
import model.musicos.*
import model.instrumentos.*
import model.canciones.*
import model.presentaciones.*

describe "Test musicos con canciones y presentaciones"{
	var joaquin = new DeGrupo("Pimpinela", 20, 5)
	var lucia = new VocalistaPopular("Pimpinela", 70, "familia")
	var kike = new DeGrupo(null, 60, 20)
	var soledad = new VocalistaPopular(null, 55, "amor")
	var cisne = new Cancion(312, "Hoy el viento se abrio quedo vacio el aire una vez mas y el manantial broto y nadie esta aqui y puedo ver que solo estallan las hojas al brillar")
	var laFamilia = new Cancion(264, "Quiero brindar por mi gente sencilla, por el amor brindo por la familia")
	var presentacionLunaPark = new Presentacion (lunaPark, new wollok.lang.Date(20, 4, 2017))
	var presentacionLaTrastienda = new Presentacion (laTrastienda, new wollok.lang.Date(15, 11, 2017))
	
	fixture {
		presentacionLunaPark.agregarMusico(joaquin)
		presentacionLunaPark.agregarMusico(lucia)
		presentacionLunaPark.agregarMusico(luisAlberto)
		presentacionLaTrastienda.agregarMusico(joaquin)
		presentacionLaTrastienda.agregarMusico(lucia)
		presentacionLaTrastienda.agregarMusico(luisAlberto)
	}
	
	test "Musico DeGrupo con un grupo calcula bien habilidad"{
		assert.equals(25, joaquin.habilidad())
	}
	
	test "Joaquin si se convierte en solista posee habilidad 20"{
		joaquin.lanzarSolista()
		assert.equals(20, joaquin.habilidad())
	}
	
	test "Lucia que canta en el grupo Pimpinela posee habilidad 50"{
		assert.equals(50, lucia.habilidad())
	}
	
	test "Lucia si se convierte en solista posee habilidad 70"{
		lucia.lanzarSolista()
		assert.equals(70, lucia.habilidad())
	}
	
	test "Luis Alberto tocando con la Fender posee habilidad 80"{
		luisAlberto.guitarra(fender)
		assert.equals(80, luisAlberto.habilidad())
	}
	
	test "Luis Alberto tocando con la Gibson sana posee habilidad 100"{
		luisAlberto.guitarra(gibson)
		assert.equals(100, luisAlberto.habilidad())
	}
	
	test "Luis Alberto tocando con la Gibson rota posee habilidad 100"{
		gibson.romperse()
		luisAlberto.guitarra(gibson)
		assert.equals(40, luisAlberto.habilidad())
	}
	
	test "Joaquin no interpreta bien la cancion La Familia"{
		assert.notThat(joaquin.interpretaBien(laFamilia))
	}
	
	test "Joaquin interpreta bien la cancion Cisne"{
		assert.that(joaquin.interpretaBien(cisne))
	}
	
	test "Lucia interpreta bien la cancion La Familia"{
		assert.that(lucia.interpretaBien(laFamilia))
	}
	
	test "Lucia no interpreta bien la cancion Cisne"{
		assert.notThat(lucia.interpretaBien(cisne))
	}
	
	test "Luis Alberto interpreta bien la cancion La Familia"{
		assert.that(luisAlberto.interpretaBien(laFamilia))
	}
	
	test "Luis Alberto interpreta bien la cancion Cisne"{
		assert.that(luisAlberto.interpretaBien(cisne))
	}
	
	test "Joaquin cobra 50 por la presentacion del Luna Park"{
		assert.equals(50, joaquin.costoPresentacion(presentacionLunaPark))
	}
	
	test "Joaquin cobra 100 por la presentacion de la Trastienda del 15/11/2017 actuando solo"{
		presentacionLaTrastienda.removerMusico(lucia)
		presentacionLaTrastienda.removerMusico(luisAlberto)
		assert.equals(100, presentacionLaTrastienda.costo())
	}
	
	test "Lucia cobra 500 por la presentacion del Luna Park"{
		assert.equals(500, lucia.costoPresentacion(presentacionLunaPark))
	}
	
	test "Lucia cobra 400 por la presentacion de la Trastienda"{
		assert.equals(400, lucia.costoPresentacion(presentacionLaTrastienda))
	}
	
	test "Luis Alberto cobra 1000 por la presentacion del Luna Park"{
		assert.equals(1000, luisAlberto.costoPresentacion(presentacionLunaPark))
	}
	
	test "Luis Alberto cobra 1200 por la presentacion de la Trastienda"{
		assert.equals(1200, luisAlberto.costoPresentacion(presentacionLaTrastienda))
	}
	
	test "El costo de la presentacion del Luna Park es de 1550"{
		assert.equals(1550, presentacionLunaPark.costo())
	}
	
	test "El costo de la presentacion de la Trastienda el 15/11/2017 es de 1650"{
		assert.equals(1650, presentacionLaTrastienda.costo())
	}
}