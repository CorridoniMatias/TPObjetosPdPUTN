import model.estadios.*
import model.musicos.*
import model.instrumentos.*
import model.canciones.*
import model.presentaciones.*
import model.albumes.*

describe "Test musicos con canciones y presentaciones"{
	var joaquin = new MusicoDeGrupo("Pimpinela", 20, 5)
	var lucia = new MusicoVocalistaPopular("Pimpinela", 70, "familia")
	var kike = new MusicoDeGrupo(null, 60, 20)
	var soledad = new MusicoVocalistaPopular(null, 55, "amor")
	var cisne = new Cancion(312, "Hoy el viento se abrio quedo vacio el aire una vez mas y el manantial broto y nadie esta aqui y puedo ver que solo estallan las hojas al brillar")
	var laFamilia = new Cancion(264, "Quiero brindar por mi gente sencilla, por el amor brindo por la familia")
	var presentacionLunaPark = new Presentacion (lunaPark, new wollok.lang.Date(20, 4, 2017))
	var presentacionLaTrastienda = new Presentacion (laTrastienda, new wollok.lang.Date(15, 11, 2017))
	var especialLaFamilia = new Album("Especial La Familia", new Date(17,6,1992), 100000, 89000)
	var laSole = new Album("La Sole", new Date(4, 2, 2005), 200000, 130000)
	var eres = new Cancion(145, "Eres lo mejor que me paso en la vida, no tengo duda, no habra mas nada despues de ti. Eres lo que le dio brillo al dia a dia, y asi sera por siempre, no cambiara, hasta el final de mis dias")
	var corazonAmericano = new Cancion(154, "Canta corazon, canta mas alto, que tu pena al fin se va marchando, el nuevo milenio ha de encontrarnos, junto corazon, como so√±amos.")
	var paraLosArboles = new Album("Para los Arboles", new Date(31,3,2003), 50000, 49000)
	var almaDeDiamante = new Cancion(216, "Ven a mi con tu dulce luz alma de diamante. Y aunque el sol se nuble despues sos alma de diamante. Cielo o piel silencio o verdad sos alma de diamante. Por eso ven asi con la humanidad alma de diamante")
	var justCrisantemo = new Album("Just Crisantemo", new Date(5, 12, 2007), 28000, 27500)
	var crisantemo = new Cancion(175, "Tocame junto a esta pared, yo quede por aqui... cuando no hubo mas luz... quiero mirar a traves de mi piel... Crisantemo, que se abrio... encuentra el camino hacia el cielo")
	fixture {
		presentacionLunaPark.agregarMusico(joaquin)
		presentacionLunaPark.agregarMusico(lucia)
		presentacionLunaPark.agregarMusico(luisAlberto)
		presentacionLaTrastienda.agregarMusico(joaquin)
		presentacionLaTrastienda.agregarMusico(lucia)
		presentacionLaTrastienda.agregarMusico(luisAlberto)
		especialLaFamilia.agregarCancion(laFamilia)
		joaquin.agregarAlbum(especialLaFamilia)
		laSole.agregarCancion(eres)
		laSole.agregarCancion(corazonAmericano)
		soledad.agregarAlbum(laSole)
		paraLosArboles.agregarCancion(cisne)
		paraLosArboles.agregarCancion(almaDeDiamante)
		justCrisantemo.agregarCancion(crisantemo)
		luisAlberto.agregarAlbum(paraLosArboles)
		luisAlberto.agregarAlbum(justCrisantemo)
	}
	
	test "Musico DeGrupo con un grupo calcula bien habilidad"{
		assert.equals(25, joaquin.habilidad())
	}
	
	test "Habilidad de un Musico DeGrupo que se vuelve solista cambia"{
		joaquin.lanzarSolista()
		assert.equals(20, joaquin.habilidad())
	}
	
	test "Habilidad de un Musico que es vocalista popular"{
		assert.equals(50, lucia.habilidad())
	}
	
	test "Habilidad de un Musico que es vocalista popular y se convierte a solista"{
		lucia.lanzarSolista()
		assert.equals(70, lucia.habilidad())
	}
	
	test "Habilidad de Musico con cierto instrumento"{
		luisAlberto.guitarra(fender)
		assert.equals(80, luisAlberto.habilidad())
	}
	
	test "Habilidad de Musico con cierto instrumento sano"{
		luisAlberto.guitarra(gibson)
		assert.equals(100, luisAlberto.habilidad())
	}
	
	test "Habilidad de Musico con cierto instrumento roto"{
		gibson.romperse()
		luisAlberto.guitarra(gibson)
		assert.equals(40, luisAlberto.habilidad())
	}
	
	test "Un musico de grupo no interpreta bien una cancion (larga)"{
		assert.notThat(joaquin.interpretaBien(laFamilia))
	}
	
	test "Un musico de grupo interpreta bien una cancion (corta)"{
		assert.that(joaquin.interpretaBien(cisne))
	}
	
	test "Un musico vocalista popular interpreta bien una cancion"{
		assert.that(lucia.interpretaBien(laFamilia))
	}
	
	test "Un musico vocalista popular no interpreta bien una cancion"{
		assert.notThat(lucia.interpretaBien(cisne))
	}
	
	test "Luis Alberto interpreta bien la cancion La Familia"{
		assert.that(luisAlberto.interpretaBien(laFamilia))
	}
	
	test "Luis Alberto interpreta bien la cancion Cisne"{
		assert.that(luisAlberto.interpretaBien(cisne))
	}
	
	test "Cobro de un musico de grupo en una presentacion actuando con su grupo"{
		assert.equals(50, joaquin.costoPresentacion(presentacionLunaPark))
	}
	
	test "Cobro de un musico de grupo en una presentacion actuando solo"{
		presentacionLaTrastienda.removerMusico(lucia)
		presentacionLaTrastienda.removerMusico(luisAlberto)
		assert.equals(100, presentacionLaTrastienda.costo())
	}
	
	test "Cobro de un musico vocalista popular en estadio con mucha capacidad"{
		assert.equals(500, lucia.costoPresentacion(presentacionLunaPark))
	}
	
	test "Cobro de un musico vocalista popular en estadio con poca capacidad"{
		assert.equals(400, lucia.costoPresentacion(presentacionLaTrastienda))
	}
	
	test "Luis Alberto cobra 1000 por la presentacion del Luna Park"{
		assert.equals(1000, luisAlberto.costoPresentacion(presentacionLunaPark))
	}
	
	test "Luis Alberto cobra 1200 por la presentacion de la Trastienda"{
		assert.equals(1200, luisAlberto.costoPresentacion(presentacionLaTrastienda))
	}
	
	test "El costo de la presentacion de un estadio de gran capacidad"{
		assert.equals(1550, presentacionLunaPark.costo())
	}
	
	test "El costo de la presentacion de un estadio determinada fecha"{
		assert.equals(1650, presentacionLaTrastienda.costo())
	}
	
	test "Reconoce si una cancion es corta"
	{
		assert.that(crisantemo.esMinimalista())
	}
	
	test "Reconoce si una cancion no es corta"
	{
		assert.notThat(almaDeDiamante.esMinimalista())
	}
	
	test "Reconoce si un musico es minimalista"
	{
		assert.that(soledad.esMinimalista())
	}
	
	test "Reconoce si un musico con 2 albumes no es minimalista"
	{
		assert.notThat(luisAlberto.esMinimalista())
	}
	
	test "Reconoce si otro musico con 1 album no es minimalista"
	{
		assert.notThat(joaquin.esMinimalista())
	}
	
	test "Reconoce si las canciones de un musico poseen cierta palabra"
	{
		assert.equals(#{crisantemo, almaDeDiamante}, luisAlberto.cancionesConPalabra("cielo"))
	}
	
	test "Reconoce cuando un musico no tiene canciones con cierta palabra"
	{
		assert.equals(#{}, luisAlberto.cancionesConPalabra("pujante"))
	}
	
	test "Reconoce la duracion de obras de un musico sin albumes"
	{
		assert.equals(0, kike.duracionObra())
	}
	
	test "Reconoce la duracion de obras de un musico con 1 album"
	{
		assert.equals(299, soledad.duracionObra())
	}
	
	test "Reconoce la duracion de obras de un musico con 2 albumes"
	{
		assert.equals(703, luisAlberto.duracionObra())
	}
	
	test "Reconoce la cancion mas larga de un album"
	{
		assert.equals(almaDeDiamante, paraLosArboles.cancionMasLarga())
	}
	
	test "Reconoce la cancion mas larga de un album que contiene 2 canciones"
	{
		assert.equals(eres, laSole.cancionMasLarga())
	}
	
	test "Reconoce la cancion mas larga de un album que contiene solo 1 cancion"
	{
		assert.equals(laFamilia, especialLaFamilia.cancionMasLarga())
	}
	
	test "Reconoce si un artista con 2 albumes la pego"
	{
		assert.that(luisAlberto.laPego())
	}
	
	test "Reconoce si un artista con 1 album no la pego"
	{
		assert.notThat(soledad.laPego())
	}
	
	test "Reconoce si un artista con 1 album la pego"
	{
		assert.that(joaquin.laPego())
	}
	
	
}